# utility functions

## `_utils.mkVhost`
`freeformAttrset -> freeformAttrset`
make a virtual host with sensible defaults

pass in a set to override the defaults.

### Example
```nix
services.nginx.virtualHosts."balls.example" = _utils.mkVhost {};
```

## `_utils.mkSimpleProxy`
`{port, protocol, location, websockets, extraConfig} -> freeformAttrset`

make a simple reverse proxy

takes a set:
```nix
{
  port,
  protocol ? "http",
  location ? "/",
  websockets ? false,
  extraConfig ? {}
}
```

It is recommended to override/add attributes with `extraConfig` to
preserve defaults.

Items in `extraConfig` are merged verbatim to the base attrset with defaults.
They are overridden based on their priority order.

## `_utils.genSecrets`
`namespace<str> -> files<list[str]> -> value<attrset> -> attrset`

generate an attrset to be passed into sops.secrets.

### Example
```nix
{ _utils, ... }:
let
  secrets = [
    "secure_secret"
    # this is a directory structure, so secrets will be stored as a file in /run/secrets/service/test/secret.
    "service/test/secret"
  ];
in {
  sops.secrets = _utils.genSecrets "" secrets {}; # it's recommended to use a namespace, but having none is still fine.
  # -> sops.secrets."secure_secret" = {};
  #    sops.secrets."service/test/secret" = {};
  sops.secrets = _utils.genSecrets "balls" ["balls_secret"] {owner = "balls";};
  # -> sops.secrets."balls/balls_secret" = {owner = "balls";};
}
```

See https://github.com/soopyc/nix-on-koumakan/blob/b7983776143c15c91df69ef34ba4264a22047ec6/systems/koumakan/services/fedivese/akkoma.nix#L8-L34 for a more extensive example

## `_utils.setupSecrets`
`attrset<nixos config attr> -> {namespace<str> ? "", secrets[list<str>], config ? freeformAttrset} -> secretHelpers`
This is a more convoluted setup that wraps around `_utils.genSecrets` to provide some additional helper functions.
Usage of this function should make more sense than just using `genSecrets`.

NOTE: `<ReturnValue>.generate` is not actually a function. The attrset is "already" "rendered" should it be actually
resolved by not being ignored by lazy eval.

NOTE: does not support overriding config for only 1 path. might implement when demand arises.

The definition of `secretHelpers` is defined as follows:
```nix
secretHelpers = {
  generate    = {}; # => {sops.secrets.* = <sopsConfig>} (inline module)
  get         = path: ""; # => actual path of the secret, usually /run/secrets/the/secret

  placeholder = path: ""; # => placeholder string generated by sops-nix, for that secret path to be used in templates.
  getTemplate = file: ""; # => actual path of the template, realized at activation time, similar to the get function.
  mkTemplate  = file: content: {}; # => {sops.templates.* = ...;}
  #             ^ => filename of the template. just an arbitrary string.
}
```

### Example
```nix
{ _utils, config, ... }: let
  secrets = _utils.setupSecrets config {
    namespace = "balls";  # for us, the namespace is just the top level element in our secrets yaml file.
    config = {
      owner = "jane";
    };
    secrets = [ "my/definitions/gock" "my/sizes/gock" ];
  };
in {
  imports = [
    secrets.generate
    (secrets.mkTemplate "my-secret.env" ''
      MY_GOCK_SIZE=${secrets.placeholder "my/sizes/gock"}
    '')
  ];

  some.service.settings.gock.file = secrets.get "my/definitions/gock";  # resolves to the path of balls/my/definitions/gock.
  some.service.settings.envFile = secrets.getTemplate "my-secret.env";
}
```

## `_utils.mkNginxFile`
`{filename<str> ? "index.html", content<str>, status<int> ? 200} -> {alias<str>, tryFiles<str>}`
Simple helper function to generate an attrset compatible with a nginx vhost `locations` attribute that serves a single file.

### Example
```nix
services.nginx.virtualHosts."example.com".locations."/" = _utils.mkNginxFile {
  content = ''
    <!doctype html><html><body>We've been trying to reach you about your car's Extended Warranty.</body></html>
  '';
};
```

## `_utils.mkNginxJSON`
`filename<str> -> freeformAttrset ==> attrset`

Simple wrapper around `_utils.mkNginxFile` that takes in an attrset and formats it as JSON.\

Note that the function signature is different in that it doesn't take in only one attrset.
This may change in the future.
